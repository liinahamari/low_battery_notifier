import com.android.build.api.dsl.CommonExtension
import io.gitlab.arturbosch.detekt.Detekt

plugins {
    id 'com.android.application' version '7.3.1' apply false
    id 'com.android.library' version '7.3.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.6.20' apply false
    id "io.gitlab.arturbosch.detekt" version "1.22.0"
}

ext {
    minSdkVersion = 23
    compileSdkVersion = 33
    targetSdkVersion = 33

    lifecycleVersion = '2.5.1'
    mockkVersion = '1.13.3'
    daggerVersion = '2.40'
    deggerProcessorVersion = '2.15'
    materialDialogsVersion = '3.3.0'
}

def setupDefaultModuleConfig(CommonExtension it) {
    it.compileSdkVersion rootProject.ext.compileSdkVersion

    it.defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    it.buildTypes {
        debug {
            minifyEnabled true
            debuggable true
        }
    }

    it.compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    it.kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        freeCompilerArgs = ['-Xjvm-default=all']
    }

    it.testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests.returnDefaultValues = true
        unitTests {
            includeAndroidResources = true
        }
    }
}

tasks.register("detektEverything", Detekt.class) { detekt ->
    detekt.reports {
        txt.enabled = false
        sarif.enabled = false
        xml.enabled = false
        html.enabled = true
    }
    detekt.parallel = true
    detekt.buildUponDefaultConfig = true
    detekt.setSource(files(projectDir))
    detekt.include("**/*.kt")
    detekt.include("**/*.kts")
    detekt.exclude("resources/")
    detekt.exclude(".idea/")
    detekt.exclude("build/")
    detekt.config.from("$rootDir/config/detekt/detekt.yml")
}
